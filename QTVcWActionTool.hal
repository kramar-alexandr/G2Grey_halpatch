external outer function Integer TestAcceptanceStatus(Integer);
external procedure GetPRName(string,var string);
external procedure TBBUSumup(var record TBBUVc);
external procedure FillupTaxMatrix(string,string,string,string,var record TaxMatrixVc);
external procedure RemoveOldProjectObjects(string,var string);
external function Boolean HasSalesmanChanged(string,string);
external function string 60 AddObjectToObjectList(string,string);
external procedure ExtractObj(string,var Integer,var string);
external procedure AddObj(string,var string);
external function Boolean CostPriceForGP(string,string,string,string,var val);
external procedure QTVc_PastePrice(var record QTVc,Integer,var Boolean);
external procedure GetUserObject(string,var string);
external procedure RemoveOldPersObjects(string,var string);
external procedure GetPriceList(string,var string,var Integer);
external procedure RemoveOldCustObjects(string,var string);
external procedure SetSalesMan(var string,string);
external function Boolean GetPD(var record PDVc);
external function Boolean FindCustomerRelation(string,string,var record CUVc);
external function string 255 FindINObjects(string,string);
external function string 20 GetTax2Acc(string);
external procedure GetTaxesDiscounts(string,Date,string,string,var val,var val);
external procedure QTVc_PasteSalesMan(var record QTVc,string);
external procedure RemoveOldUserObjects(string,var string);
external function val CalculateSerialNrQuantity(string,string,string,Boolean,val,val,val,val);
external procedure SetDelAddr(string,string,var string,var string,var string,var string,var string,var string,var string,var string);
external procedure SaveExchangeRate(val,val,val,val,val,var val,var val,var val,var val,var val);
external function Boolean OtherExchangeRate(val,val,val,val,val,val,val,val,val,val);
external function val MulRateToBase1(var string,val,val,val,val,val,val,roundmode);
external procedure CalcSum(val,val,val,val,var val,Integer);
external procedure CalcProc(val,val,var val);
external procedure FindSalesExVat(record TaxMatrixVc,string,val,Integer,Integer,var val);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external procedure QTDchsum(record QTVc,Integer);
external procedure SerialNrDimensions(string,string,var val,var val,var val);
external function Integer CheckSerialStatus(string,string,var string);
external procedure PasteSalesManInQT(var record QTVc);
external procedure GetBaseCurncy(Integer,var string);

external procedure GetPayDealText(string,string,var string);
external procedure FindUserMailboxName(string,var string,var string);
external function Boolean GetItemNameStr(Integer,var string,string,string,string);
external procedure GetProjectPriceAndCost(string,string,string,string,string,Integer,var val,var val,var string,var val,var val,var string);
external function Boolean SetUnitFactor(string,var val,val,val,val,var val,var val,var val,Boolean);
external function Boolean GetItemPriceDiscount3(string,val,var record INVc,string,val,val,val,val,val,string,string,string,string,
                                                var val,var string,var val,var string,var val,var string,Integer,var Boolean,Date,
                                                string,Boolean,var Boolean,string,var string,var val,string,string,var string);
external function Boolean QtyDepPriceL(string);
external procedure QTGetCalcItemPrice(record INVc,record QTVc,Integer,var val);
external function Boolean QTVc_ExplodeRecepy(record INVc,val,record QTVc,string,Integer);
external function Boolean QTDchrsum(var record QTVc,Integer,Integer);
external procedure QTSumup(var record QTVc);
external procedure GetFullCurncyRate (var string,Date,var val,var val,var val,var val,var val);
external function Boolean INVc_AllowSales(record INVc,var LongInt);
external function string 20 FindCustRoute(string,string,string);

global
procedure QTGetRate(var record QTVc QTp)
BEGIN
  val from,to1,to2,base1,base2;
  val sum;
  string 5 crncy;
 
  crncy = QTp.CurncyCode;
  GetFullCurncyRate(crncy,QTp.QTDate,from,to1,to2,base1,base2);
  QTp.CurncyCode = crncy;
  QTp.FrRate = from;
  QTp.ToRateB1 = to1;
  QTp.ToRateB2 = to2;
  QTp.BaseRate1 = base1;
  QTp.BaseRate2 = base2;  
  sum = MulRateToBase1(QTp.CurncyCode,QTp.Sum4,from,to1,to2,base1,base2,DefaultCurRoundOff);
  QTp.BaseSum4 = sum;  
  RETURN;
END;

global
function Boolean PasteCustInQT(var record QTVc QTp,string custcode,string efstr,var string warning,Boolean updateallf)
BEGIN
  record RebVc Rebr;
  record CUVc CUr;
  Boolean res;
  string 255 tstr,tstr2;
  Integer i;
  record PDVc PDr;
  record CCatVc CCatr;

  if ((efstr!=QTp.CustCode) and (nonblank(efstr))) then begin
    tstr = QTp.Objects;
    RemoveOldCustObjects(efstr,tstr);
    QTp.Objects = tstr;
  end;  
  warning = "";
  CUr.Code = custcode;
  if (ReadFirstMain(CUr,1,true)) then begin
    res = true;
    CCatr.Code = CUr.CustCat;
    if (ReadFirstMain(CCatr,1,true)) then begin end;
    Rebr.Code = CUr.RebCode;
    if (blank(Rebr.Code)) then begin
      Rebr.Code = CCatr.RebCode;
    end;
    if (ReadFirstMain(Rebr,1,true)) then begin
      QTp.DiscPerc = Rebr.DiscPerc;
    end;
    QTp.Addr0 = CUr.Name;
    QTp.Addr1 = CUr.InvAddr0;
    QTp.Addr2 = CUr.InvAddr1;
    QTp.Addr3 = CUr.InvAddr2;
    QTp.InvAddr3 = CUr.InvAddr3;
    QTp.InvAddr4 = CUr.InvAddr4;
    if ((nonblank(CUr.DelAddr0)) or (nonblank(CUr.DelAddr1)) or (nonblank(CUr.DelAddr2)) or (nonblank(CUr.DelAddr3)) or (nonblank(CUr.DelAddr4))) then begin
      QTp.ShipAddr0 = CUr.Name;
      QTp.ShipAddr1 = CUr.DelAddr0;
      QTp.ShipAddr2 = CUr.DelAddr1;
      QTp.ShipAddr3 = CUr.DelAddr2;
      QTp.DelAddr3 = CUr.DelAddr3;
      QTp.DelAddr4 = CUr.DelAddr4;
    end else begin  //BS should blank del address if you switch customer (as on order)
      QTp.ShipAddr0 = "";
      QTp.ShipAddr1 = "";
      QTp.ShipAddr2 = "";
      QTp.ShipAddr3 = "";
      QTp.DelAddr3 = "";
      QTp.DelAddr4 = "";
    end;
    QTp.ExportFlag = CUr.ExportFlag;
    QTp.CustCat = CUr.CustCat;
    QTp.PayDeal = CUr.PayDeal;
    if ((nonblank(QTp.Objects)) and (nonblank(CUr.Objects))) then begin
      QTp.Objects = QTp.Objects & ",";
    end;
    QTp.Objects = QTp.Objects & CUr.Objects;
    QTp.CurncyCode = CUr.CurncyCode;
    QTp.LangCode = CUr.LangCode;
    QTp.InvoiceToCode = CUr.InvoiceToCode;
    QTp.ShipDeal = CUr.ShipDeal;
    QTp.ShipMode = CUr.ShipMode;
    QTp.Comment = CUr.OrderComment;
    QTp.PriceList = CUr.PLCode;
    if (blank(QTp.PriceList)) then begin
      QTp.PriceList = CCatr.PLCode;
    end;
    QTp.RebCode = CUr.RebCode;
    if (blank(QTp.RebCode)) then begin
      QTp.RebCode = CCatr.RebCode;
    end;
    tstr = QTp.PriceList;
    GetPriceList(QTp.CustCat,tstr,i);
    QTp.InclVAT = i;
    QTp.NoTax1 = CUr.NoTax1;
    QTp.NoTax2 = CUr.NoTax2;
    if (nonblank(tstr)) then begin
      QTp.PriceList = tstr;
    end;
    if (blank(QTp.PriceList)) then begin
      PDr.Code = QTp.PayDeal;
      if (GetPD(PDr)) then begin
        QTp.PriceList = PDr.PriceList;
      end;
    end;
    QTp.CustVATCode = CUr.VATCode;
    QTp.CustTaxTemplateCode = CUr.TaxTemplateCode;
    QTp.Phone = CUr.Phone;
    QTp.Fax = CUr.Fax;
    QTp.VATNr = CUr.VATNr;
    QTp.CustVATCode = CUr.VATCode;
    QTp.Region = CUr.Region;
    warning = CUr.WarnText1;
    QTGetRate(QTp);    
    if (updateallf) then begin
      QTp.Sorting = FindCustRoute(QTp.CustCode,QTp.Location,QTp.DelAddrCode);
      QTp.CustContact = CUr.Person;
      SetSalesMan(tstr,CUr.SalesMan);

      tstr2 = QTp.Objects;
      RemoveOldPersObjects(QTp.SalesMan,tstr2);
      QTp.Objects = tstr2;
      QTp.SalesMan = tstr;
      PasteSalesManInQT(QTp);
      GetUserObject(QTp.SalesMan,tstr);    
      if ((nonblank(QTp.Objects)) and (nonblank(tstr))) then begin
        QTp.Objects = QTp.Objects & ",";
      end;
      QTp.Objects = QTp.Objects & tstr;
    end;
  end;  
  PasteCustInQT = res;
  RETURN;
END;

global
procedure QTMultiLineText(record QTVc QTr,record INVc INr,string  langcode,Integer arow)
BEGIN
  row QTVc QTrw;
  string 255 tstr;
  Integer tcnt;
 
  tcnt = 2;
  while (GetItemNameStr(tcnt,tstr,langcode,INr.Name,INr.Code)) begin      
    ClearRow(QTr,QTrw,1);
    QTrw.Spec = tstr;
    MatRowInsert(QTr,arow+tcnt-1,QTrw);
    tcnt = tcnt + 1;
  end;
  RETURN;
END;

global
procedure QTVc_PasteQuant(var record QTVc QTr,Integer rownr,var Boolean chsum)
BEGIN
  record INVc INr;
  row QTVc QTrw;
  val t,s,price,vreb,baseprice,temp,tax2prc;
  string 255 curitemname,salesacc,vatcode,tax2code,taxtemplatecode;
  Boolean calcprice,chrsum;
  Boolean pritemf;
  string 10 markup;
  record GeneralOptionBlock GenOptRec;
  Boolean dummyf;

  price = QTrw.Price;
  BlockLoad(GenOptRec);
  chsum = false;
  chrsum = false;
  MatRowGet(QTr,rownr,QTrw);
  price = QTrw.Price;
  vreb = QTrw.vRebate;
  if (GetItemPriceDiscount3(QTrw.ArtCode,QTrw.Quant,INr,QTr.CurncyCode,
                            QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,
                            QTr.LangCode,QTr.CustCat,QTr.PriceList,QTr.RebCode,
                            price,curitemname,vreb,vatcode,baseprice,salesacc,
                            QTr.ExportFlag,calcprice,QTr.QTDate,QTr.CustCode,false,dummyf,QTr.PayDeal,tax2code,tax2prc,QTr.Region,QTr.Location,taxtemplatecode)) then begin
    if (nonblank(QTr.PriceList)) then begin
      if (QtyDepPriceL(QTr.PriceList)) then begin
        QTrw.Price = price;
      end;
    end;
    if (calcprice) then begin
      MatRowPut(QTr,rownr,QTrw);
      temp = -1;
      QTGetCalcItemPrice(INr,QTr,rownr,temp);
      MatRowGet(QTr,rownr,QTrw);
    end;
    if (INr.ExplodeRec==0) then begin
      if ((INr.ItemType==2) and (nonblank(INr.Recepy))) then begin
        QTrw.Recepy = INr.Recepy;
      end else begin
        QTrw.Recepy = "";
      end;
    end;
    QTrw.vRebate = vreb;
    if (baseprice<>0) then begin
      QTrw.BasePrice = baseprice;
    end;
    chrsum = true;
    MatRowPut(QTr,rownr,QTrw);
    if ((INr.ExplodeRec!=0) and (nonblank(QTrw.Recepy))) then begin
      if (QTVc_ExplodeRecepy(INr,QTrw.Quant,QTr,QTrw.Recepy,rownr)) then begin
        MatRowGet(QTr,rownr,QTrw);
        QTrw.Recepy = "";
        QTrw.BasePrice = blankval;
        MatRowPut(QTr,rownr,QTrw);
      end;
    end;
  end;
  if (chrsum) then begin
    chsum = QTDchrsum(QTr,rownr,GenOptRec.UseDiscount);
  end;
  RETURN;
END;

global
function Boolean QTVc_PasteArtCode(var record QTVc QTr,Integer rownr,var string inwarning,var string warning)
BEGIN
  Boolean chsum,res;
  Boolean chrsum;
  record INVc INr;
  row QTVc QTrw;
  val t,s,price,vreb,baseprice,temp,tax2prc,tax2reb,priceincur;
  string 255 curitemname,salesacc,vatcode,tax2code,taxtemplatecode;
  Boolean calcprice;
  Boolean pritemf;
  val UnitFactQuant,UnitFactPrice,Quant;
  string 10 markup,markupincur;
  record GeneralOptionBlock GenOptRec;
  Boolean dummyf;
  LongInt ErrorCode;
  record TaxMatrixVc TMr;

  BlockLoad(GenOptRec);
  chsum = false;
  chrsum = false;
  pritemf = true;
  MatRowGet(QTr,rownr,QTrw);
  QTrw.Price = BlankVal;
  QTrw.vRebate = BlankVal;
  QTrw.Sum = BlankVal;
  if (GetItemPriceDiscount3(QTrw.ArtCode,QTrw.Quant,INr,QTr.CurncyCode,
                            QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,
                            QTr.LangCode,QTr.CustCat,QTr.PriceList,QTr.RebCode,
                            price,curitemname,vreb,vatcode,baseprice,salesacc,
                            QTr.ExportFlag,calcprice,QTr.QTDate,QTr.CustCode,false,dummyf,Qtr.PayDeal,tax2code,tax2prc,QTr.Region,QTr.Location,taxtemplatecode)) then begin
    if (nonblank(QTr.CustTaxTemplateCode)) then begin
      taxtemplatecode = QTr.CustTaxTemplateCode;
    end;
    FillupTaxMatrix(QTr.CustCode,QTr.CustCat,QTr.DelAddrCode,taxtemplatecode,TMr);
    if (vreb==0) then begin
      vreb = blankval;
    end;
    QTrw.Price = price;
    QTrw.Spec = curitemname;
    QTrw.vRebate = vreb;
    QTrw.VATCode = vatcode;
    QTrw.TaxTemplateCode = taxtemplatecode;
    QTrw.BasePrice = baseprice;
    QTrw.SalesAcc = salesacc;
    QTrw.ArtCode = INr.Code;
    QTrw.Objects = FindINObjects(INr.Objects,INr.Group);
    QTrw.UnitCode = INr.Unittext;
    QTrw.UnitXval = INr.Width;
    QTrw.UnitYval = INr.Height;
    QTrw.UnitZval = INr.Depth;
    QTrw.PriceFactor = INr.PriceFactor;
    GetTaxesDiscounts(QTr.CustCode,QTr.QTDate,QTrw.VATCode,tax2code,t,tax2reb);
    if (QTr.NoTax2==0) then begin
      QTrw.TAX2Code = tax2code;
      QTrw.TAX2Prc = tax2prc;
      QTrw.TAX2Reb = tax2reb;
      QTrw.TAX2Acc = GetTax2Acc(QTrw.TAX2Code);
    end;
    if (QTr.NoTax1==0) then begin
      QTrw.TAX1Reb = t;
    end;
    if (nonblank(QTrw.UnitCode)) then begin
      price = QTrw.Price;
      Quant = QTrw.Quant;
      chrsum = true;
      if (nonblank(QTrw.UnitCode)) then begin
        chrsum = SetUnitFactor(QTrw.UnitCode,UnitFactQuant,QTrw.UnitXval,QTrw.UnitYval,QTrw.UnitZval,UnitFactPrice,Quant,price,false);
      end;
      if (chrsum) then begin
        QTrw.UnitFactQuant = UnitFactQuant;
        QTrw.UnitFactPrice = UnitFactPrice;
        QTrw.Quant = Quant;
        QTrw.Price = price;
      end;
    end;
    switch (INr.ItemType) begin
      case 1: QTrw.RowType = QTr.BudStocked;
      case 3: QTrw.RowType = QTr.BudTime;
      otherwise 
        if (INr.ItemMaterial!=0) then begin
          QTrw.RowType = QTr.BudMaterial;
        end else begin
          QTrw.RowType = QTr.BudOther;
        end;  
    end;
    QTrw.ItemType = INr.ItemType;
    if (QTrw.ItemType==0) then begin
      if (INr.ItemMaterial!=0) then begin
        QTrw.ItemType = 5;
      end;
    end;   
    if (nonblank(QTr.PRCode)) then begin 
      GetProjectPriceAndCost(QTr.PRCode,QTrw.ArtCode,QTrw.EMCode,QTrw.TimeClass,QTr.CurncyCode,QTrw.ItemType,
                             price,vreb,markup,baseprice,priceincur,markupincur);
      if (priceincur!=0) then begin
        QTrw.Price = priceincur;
      end;
      if (price!=0) then begin
        QTrw.Price = price;
      end;
      if (vreb!=0) then begin
        QTrw.vRebate = vreb;
      end;
      if (nonblank(markupincur)) then begin
        QTrw.Markup = markupincur;
      end;
      if (nonblank(markup)) then begin
        QTrw.Markup = markup;
      end;
      if (baseprice!=0) then begin
        QTrw.BasePrice = baseprice;
      end;
    end;      
    if ((INr.ItemType==2) and (nonblank(INr.Recepy))) then begin
      QTrw.Recepy = INr.Recepy;
    end;
    if (nonblank(QTr.CustVATCode)) then begin
      QTrw.VATCode = QTr.CustVATCode;
    end;
    if (calcprice) then begin
      MatRowPut(QTr,rownr,QTrw);
      temp = -1;
      QTGetCalcItemPrice(INr,QTr,rownr,temp);
      MatRowGet(QTr,rownr,QTrw);
    end;
    //PackRowFieldMatrix(QTrw,"TaxMatrix",TMr);
    MatRowPut(QTr,rownr,QTrw);
    chrsum = true;
    QTMultiLineText(QTr,INr,QTr.LangCode,rownr);
    if (nonblank(INr.WarnText1)) then begin
      inwarning = INr.WarnText1;
    end;
    if (!INVc_AllowSales(INr,ErrorCode)) then begin
      warning = USetStr(ErrorCode);
    end;
    if (QTDchrsum(QTr,rownr,GenOptRec.UseDiscount)) then begin
      QTDchsum(QTr,rownr);
    end;    
    QTSumup(QTr);
    MatRowGet(QTr,rownr,QTrw);
    res = true;
  end else begin
    if (nonblank(QTrw.ArtCode)) then begin
      warning = USetStr(1120) & QTrw.ArtCode;
    end;
  end;
  QTVc_PasteArtCode = res;
  RETURN;
END;

global
updating function Integer CreateMailFromQTD(record QTVc QTr,var record MailVc Mailr,var string warning,string pdfform)
BEGIN
  Integer res;
  record LTxtVc LTxtr;
  record CUVc CUr;
  record CUVc ContactCUr;  
  row QTVc QTrw;  
  row MailVc MArw;
  Integer i,rwcnt;
  string 255 mailboxnr,tstr;
  string 1 TAB;
  record QTMailBlock QTMailr;
  Integer line;
  string 255 docname,fname;
  record UserVc USr;
  SetLangMode(LangUkrainian,"UKR",0);// 62
  res = 0;
  if (ReadFirstMain(QTr,0,true)) then begin end;  
  TAB = chr(9);
  // FindUserMailboxName(CurrentUser,mailboxnr,tstr);
  // if (blank(tstr)) then begin
    // res = 22010;
    // warning = CurrentUser;
    // goto LCreateMailFromQTD;
  // end;  
  //MArw.AddrCode = tstr;
  
  MArw.AddrCode = "hansa@g2.com";//SAV 6016
  MArw.RowTyp = 1;
  
  MatRowPut(Mailr,0,MArw);
  BlockLoad(QTMailr);
  CUr.Code = QTr.CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin end;
  MArw.RowTyp = 0;
  // MArw.AddrCode = CUr.eMail;
  // if (nonblank(QTr.CustContact)) then begin
    // if (FindCustomerRelation(CUr.Code,QTr.CustContact,ContactCUr)) then begin
      // if (nonblank(ContactCUr.eMail)) then begin
        // MArw.AddrCode = ContactCUr.eMail;
      // end;
    // end;
  // end;
  // if (blank(MArw.AddrCode)) then begin
    // res = 2289;
    // warning = CUr.Code;
    // goto LCreateMailFromQTD;
  // end;
  USr.Code = CUr.SalesMan;
  if (ReadFirstMain(USr,1,true)) then begin end;
  tstr = USr.emailAddr;
  if blank(tstr) then begin
	USr.Code = "FD";
	if (ReadFirstMain(USr,1,true)) then begin end;
	tstr = USr.emailAddr;
  end;
  MArw.AddrCode = tstr;//SAV 6016
  MArw.RowTyp = 0;
  MatRowPut(Mailr,1,MArw);
  
  MArw.AddrCode = "";
  CUr.Code = QTr.CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin end;
  MArw.RowTyp = 0;
  MArw.AddrCode = CUr.eMail;
  if (nonblank(QTr.CustContact)) then begin
     if (FindCustomerRelation(CUr.Code,QTr.CustContact,ContactCUr)) then begin
       if (nonblank(ContactCUr.eMail)) then begin
         MArw.AddrCode = ContactCUr.eMail;
       end;
     end;
   end;
   if (nonblank(MArw.AddrCode)) then begin     
     MatRowPut(Mailr,2,MArw);
   end;
  
  
  Mailr.Header = QTMailr.Header & " " & USetStr(1199);
  Mailr.Header = Mailr.Header & QTr.SerNr;
  LTxtr.Code = QTMailr.FirstTxt;
  if (ReadFirstMain(LTxtr,1,true)) then begin
    rwcnt = LineTextCnt(LTxtr);
    for (i=0;i<rwcnt;i=i+1) begin
      tstr = LineTextGet(LTxtr,i);
      LineTextPut(Mailr,line,tstr);
      line = line + 1;
    end;    
    AddCRToText(Mailr);
  end;
//  AddCRToText(Mailr); this doesnt work on linux 
  tstr = "";
  LineTextPut(Mailr,line,tstr);
  line = line + 1;
  rwcnt = MatRowCnt(QTr);
  
  tstr = "Товар" & TAB & TAB & "К-сть" &  TAB & TAB & "Наймен." & TAB & TAB & "Ціна" & TAB & TAB & "Сума";
  LineTextPut(Mailr,line,tstr);
  
   line = line + 1;
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(QTr,i,QTrw);
    switch (QTrw.stp) begin
      case 10:
        goto LQTHIDDENLine;
      case 17:
        tstr = TAB & TAB & Left(QTrw.Spec,60);
        LineTextPut(Mailr,line,tstr);
        line = line + 1;
      case 9:
        tstr = TAB & TAB & Left(QTrw.Spec,60) & TAB;
        tstr = tstr & QTrw.Sum & TAB;
        LineTextPut(Mailr,line,tstr);
        line = line + 1;
      otherwise
        tstr = QTrw.ArtCode & TAB;
        tstr = tstr & ValToString(QTrw.Quant,M4UVal,"",".",0) & TAB;
        tstr = tstr & Left(QTrw.Spec,60) & TAB;
        tstr = tstr & QTrw.Price & TAB;
        if (QTrw.vRebate!=0) then begin
          tstr = tstr & "-" & ValToString(QTrw.vRebate,M4UVal,"",".",0) & "%" &  TAB;
        end;
        tstr = tstr & QTrw.Sum & TAB;
        LineTextPut(Mailr,line,tstr);
        line = line + 1;
    end;
  end;
LQTHIDDENLine:;    
  AddCRToText(Mailr);
  line = line + 1;
  tstr = TAB & TAB & TAB & "=====";
  LineTextPut(Mailr,line,tstr);
  line = line + 1;

  tstr = TAB & TAB & TAB & USetStr(2685) & TAB & QTr.Sum1;
  LineTextPut(Mailr,line,tstr);
  line = line + 1;
  tstr = TAB & TAB & TAB & USetStr(2686) & TAB & QTr.Sum3;
  LineTextPut(Mailr,line,tstr);
  line = line + 1;  
  tstr = TAB & TAB & TAB & USetStr(2687) & TAB & QTr.Sum4;
  LineTextPut(Mailr,line,tstr);
  line = line + 1;  
  tstr = USetStr(7021) & " " & QTr.ValidUntilDate;
  LineTextPut(Mailr,line,tstr);
  line = line + 1;  
  GetPayDealText(QTr.PayDeal,QTr.LangCode,tstr);
  tstr = USetStr(3060) & " " & tstr;
  LineTextPut(Mailr,line,tstr);
  line = line + 1;  
  LTxtr.Code = QTMailr.LastTxt;
  if (ReadFirstMain(LTxtr,1,true)) then begin
    rwcnt = LineTextCnt(LTxtr);
    for (i=0;i<rwcnt;i=i+1) begin
      tstr = LineTextGet(LTxtr,i);
      LineTextPut(Mailr,line,tstr);
      line = line + 1;
    end;    
    AddCRToText(Mailr);
  end;
  Mailr.TransDate = CurrentDate;
  Mailr.SerNr = NextSerNr("MailVc",Mailr.TransDate,-1,false,"");
  if (RecordStore(Mailr,false)) then begin
    CreateRecordLink(Mailr,CurrentCompany,QTr,CurrentCompany);  
    CreateRecordLink(QTr,CurrentCompany,Mailr,CurrentCompany);  
  end;
  docname = pdfform;
  if (blank(docname)) then begin
    docname = "QTForm";
  end;
  fname = "tmp/" & USetStr(1820) & QTr.SerNr & ".pdf";
  SetDocumentFilename(fname);
  SetMedia(mtPdf);
  PrintDocument(QTr,docname,false);
  if (FileExists(fname)) then begin
    RecordLinkFile(fname,0,Mailr,CurrentCompany);
  end;
  SetDocumentFilename("");
  Delete_File(fname);
LCreateMailFromQTD:;  
  CreateMailFromQTD = res;
  RETURN;
END;

global
procedure RecalculetWeightVolumeQTVc(var record QTVc QTp)
BEGIN
  record INVc INr;
  row QTVc QTrw;
  Integer i,rwcnt;

  if (ReadFirstMain(QTp,0,true)) then begin end;  
  QTp.TotQty = blankval;
  QTp.TotVolume = blankval;
  QTp.TotWeight = blankval;
  rwcnt = MatRowCnt(QTp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(QTp,i,QTrw);
    QTp.TotQty = QTp.TotQty + QTrw.Quant;
    if  (ReadFirstItem(QTrw.ArtCode,INr,false,false)) then begin
      QTp.TotVolume = QTp.TotVolume + QTrw.Quant * INr.Volume;
      QTp.TotWeight = QTp.TotWeight + QTrw.Quant * INr.Weight;
    end;      
  end;
//  QTSumup(QTp); No point in this, just silly.
  RETURN;
END;     

global
updating function Integer CreateTBBUFromQTD(var record QTVc QTr,var record TBBUVc TBBUr)
BEGIN
  Integer res;
  record CUVc CUr;
  row QTVc QTrw;
  row TBBUVc TBBUrw;
  string 255 tstr;
  Integer i,rwcnt,tbrow;
  record PRVc PRr;
  
  PRr.Code = QTr.PRCode;
  ReadFIrstMain(PRr,1,true);
  TBBUr.PRCode = QTr.PRCode;
  if (nonblank(QTr.CurncyCode)) then begin
    if (nonblank(PRr.CurncyCode)) then begin
      if (PRr.CurncyCode!=QTr.CurncyCode) then begin
        res = 1582;
        goto LCreateTBBUFromQTD;
      end;
    end;
  end;
  
  if (ReadFirstMain(TBBUr,1,true)==false) then begin 
    RecordNew(TBBUr);
    TBBUr.Members = PRr.Members;
    TBBUr.SalesGroup = QTr.SalesGroup;
    TBBUr.TransDate = QTr.QTDate;
    TBBUr.CustCode = QTr.CustCode;
    CUr.Code = TBBUr.CustCode;
    if (ReadFirstMain(CUr,1,true)) then begin
    end;
    TBBUr.CustName = QTr.Addr0;
    TBBUr.PRLeader = PRr.Leader;
    TBBUr.SalesMan = QTr.SalesMan;
    TBBUr.LangCode = QTr.LangCode;
    TBBUr.PRCode = QTr.PRCode;
    GetPRName(TBBUr.PRCode,tstr);
    TBBUr.PRName = tstr;    
    TBBUr.Comment = QTr.Comment;
    TBBUr.Markup = QTr.Markup;
    TBBUr.BudTime = QTr.BudTime;
    TBBUr.BudOther = QTr.BudOther;
    TBBUr.BudStocked = QTr.BudStocked;
    TBBUr.BudMaterial = QTr.BudMaterial;
    TBBUr.SumTime = QTr.SumTime;
    TBBUr.SumStocked = QTr.SumStocked;
    TBBUr.SumMaterial = QTr.SumMaterial;
    TBBUr.SumOther = QTr.SumOther;
    TBBUr.CurncyCode = QTr.CurncyCode;
    TBBUr.FrRate = QTr.FrRate;
    TBBUr.ToRateB1 = QTr.ToRateB1;
    TBBUr.ToRateB2 = QTr.ToRateB2;
    TBBUr.BaseRate1 = QTr.BaseRate1;
    TBBUr.BaseRate2 = QTr.BaseRate2;
    rwcnt = MatRowCnt(QTr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(QTr,i,QTrw);
      if (QTrw.stp==10) then begin goto LSKIPQTROW; end;
      ClearRow(TBBUr,TBBUrw,QTrw.stp);
      TBBUrw.ArtCode = QTrw.ArtCode;
      TBBUrw.Qty = QTrw.Quant;
      TBBUrw.Price = QTrw.Price;
      TBBUrw.Sum = QTrw.Sum;
      TBBUrw.Objects = QTrw.Objects;
      TBBUrw.Comment = QTrw.Spec;
      TBBUrw.Recepy = QTrw.Recepy;
      TBBUrw.EMCode = QTrw.EMCode;
      TBBUrw.Markup = QTrw.Markup;
      TBBUrw.TimeClass = QTrw.TimeClass;
      TBBUrw.InvAfter = QTrw.InvAfter;
      TBBUrw.Invoiced = QTrw.Invoiced;
      TBBUrw.InvDate = QTrw.InvDate;
      TBBUrw.ItemType = QTrw.ItemType;
      TBBUrw.RowType = QTrw.RowType;
      TBBUrw.BudType = QTrw.BudType;
      TBBUrw.Discount = QTrw.vRebate;
      TBBUrw.Cost = QTrw.BasePrice;
      TBBUrw.GP = QTrw.rowGP;
      TBBUrw.GM = TBBUrw.GP*100/TBBUrw.Sum;      
      if (QTrw.stp==17) or (QTrw.stp==9) then begin
        TBBUrw.Comment = "";
        TBBUrw.Spec = QTrw.Spec;
      end;
      TBBUrw.PRStage = QTrw.PRStage;
      MatRowPut(TBBUr,tbrow,TBBUrw);
      tbrow = tbrow + 1;
LSKIPQTROW:;      
    end;
    TBBUSumup(TBBUr);
    if (RecordStore(TBBUr,false)) then begin 
      if (blankdate(QTr.RejectDate)) then begin
        QTr.RejectDate = CurrentDate;
      end;
      QTr.Rejected = 2;
      QTr.Probability = 100;
      res = 0;
    end;
  end else begin
    res = 2084;
  end;
LCreateTBBUFromQTD:;  
  CreateTBBUFromQTD = res;
  RETURN;
END;    



global 
updating procedure CreateTBBUFromQTDsm()
BEGIN
  Integer wn,nwn,err;
  record QTVc QTr;
  record TBBUVc TBBUr;
  Boolean testf;
  
  wn = CurWindow;
  GetWindowRecord(wn,QTr);  
  if (nonblank(QTr.PRCode)) then begin
    if ((QTr.Closed==0) and (QTr.Rejected!=1)) then begin
      testf = true;
      err = TestAcceptanceStatus(QTr.AcceptanceStatus);
      if (err!=0) then begin
        MessageBox(err,"");
        testf = false;
      end;
      if (testf) then begin
        err = CreateTBBUFromQTD(QTr,TBBUr);
        if (err==0) then begin
          PutWindowRecord(wn,QTr);
          if (WindowDoOK(wn,0)) then begin    end;
          nwn = OpenWindow("TBBUDClass",1,0,"","",TBBUr);
          UpdateBrowses("QTVc");
          UpdateBrowses("TBBUVc");
        end else begin
          MessageBox(err,"");
        end;
      end;
    end else begin
      Beep;
    end;
  end;      
  RETURN;
END;
global 
procedure QTDUpdatePrices(var record QTVc QTr,Boolean readf)

BEGIN
  Integer i,rwcnt;
  record INVc INr;
  row QTVc QTrw;
  Boolean calcprice,chsum;
  string 255 tstr,taxtemplatecode;
  string 2 vatcode;
  string 10 salesacc,markup,tax2code,markupincur;
  val price,vrebate,baseprice,tax2prc,priceincur;
  val subtot;
  record GeneralOptionBlock GenOptRec;


  Boolean dummyf;

  if (readf) then begin
    if (ReadFirstMain(QTr,0,true)) then begin end;
  end;
  BlockLoad(GenOptRec);
  rwcnt = MatRowCnt(QTr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(QTr,i,QTrw);
    switch (QTrw.stp) begin
      case 1:
    if (nonblank(QTrw.ArtCode)) then begin      
      vrebate = QTrw.vRebate;


      if (blank(QTr.PRCode)) then begin
        if (GetItemPriceDiscount3(QTrw.ArtCode,QTrw.Quant,INr,QTr.CurncyCode,
          QTr.FrRate,QTr.ToRateB1,QTr.ToRateB2,QTr.BaseRate1,QTr.BaseRate2,
          QTr.LangCode,QTr.CustCat,QTr.PriceList,QTr.RebCode,
          price,tstr,vrebate,vatcode,baseprice,
          salesacc,QTr.ExportFlag,calcprice,QTr.QTDate,QTr.CustCode,true,dummyf,QTr.PayDeal,tax2code,tax2prc,QTr.Region,QTr.Location,taxtemplatecode)) then begin
          QTrw.Price = price;
          QTrw.vRebate = vrebate;
          QTrw.BasePrice = baseprice;
          QTrw.VATCode = vatcode;
          QTrw.SalesAcc = salesacc;
          QTrw.ArtCode = INr.Code;
        end;  
      end else begin
        GetProjectPriceAndCost(QTr.PRCode,QTrw.ArtCode,QTrw.EMCode,QTrw.TimeClass,QTr.CurncyCode,QTrw.ItemType,
                               price,vrebate,markup,baseprice,priceincur,markupincur);
        if (priceincur!=0) then begin
          QTrw.Price = priceincur;
        end;
        if (price!=0) then begin
          QTrw.Price = price;
        end;
        if (vrebate!=0) then begin
          QTrw.vRebate = vrebate;
        end;
        if (nonblank(markupincur)) then begin
          QTrw.Markup = markupincur;
        end;
        if (nonblank(markup)) then begin
          QTrw.Markup = markup;
        end;



        if (baseprice!=0) then begin
          QTrw.BasePrice = baseprice;
        end;
      end;
      MatRowPut(QTr,i,QTrw);
      chsum = QTDchrsum(QTr,i,GenOptRec.UseDiscount);
      QTDchsum(QTr,i);
//        ChangeQTRowSum(QTr,i,GenOptRec.UseDiscount);
      MatRowGet(QTr,i,QTrw);
      subtot = subtot + QTrw.Sum;
    end;
      case 9:
        QTrw.Sum = subtot;
        subtot = blankval;
        MatRowPut(QTr,i,QTrw);
    end;    


  end;
  QTSumup(QTr);      
  RETURN;
END;


global
procedure QTVc_PasteCurncyCode(var record QTVc QTp,string oldcurncy)
BEGIN
  val fr,to1,to2,br1,br2;
  record ItemSettingBlock ISr;
  
  if (QTp.CurncyCode!=oldcurncy) then begin
    QTGetRate(QTp);
    QTDUpdatePrices(QTp,false);
  end else begin
    BlockLoad(ISr);
    SaveExchangeRate(QTp.FrRate,QTp.ToRateB1,QTp.ToRateB2,QTp.BaseRate1,QTp.BaseRate2,fr,to1,to2,br1,br2);
    QTGetRate(QTp);
    if ((OtherExchangeRate(QTp.FrRate,QTp.ToRateB1,QTp.ToRateB2,QTp.BaseRate1,QTp.BaseRate2,fr,to1,to2,br1,br2)) and (ISr.UpdCurPrWithDate!=0)) then begin
      QTDUpdatePrices(QTp,false);
    end;
  end;
  RETURN;
END;

global
function Boolean QTVc_PastePRCode(var record QTVc QTr,string oldprcode,var string warning)
BEGIN
  Boolean res;
  record PRVc PRr;
  record ProjectBlock PRb;
  string 10 oldsalesman;
  string 60 objs;

  //RemoveOldProjectObjects(oldprcode,QTr.Objects);// Edit ************************** Wednesday, 21 September 2016 11:25:59
  PRr.Code = QTr.PRCode;
  if (ReadFirstMain(PRr,1,true)) then begin
    if (QTr.CustCode!=PRr.CustCode) then begin
      //RemoveOldCustObjects(QTr.CustCode,QTr.Objects);// Edit ************************** Wednesday, 21 September 2016 11:25:55
      QTr.CustCode = PRr.CustCode;
      if (PasteCustInQT(QTr,QTr.CustCode,"",warning,true)) then begin
        BlockLoad(PRb);
        QTr.BudTime = PRb.BudTime;
        QTr.BudOther = PRb.BudOther;
        QTr.BudStocked = PRb.BudStocked;
        QTr.BudMaterial = PRb.BudMaterial;    
        //QTr.Objects = AddObjectToObjectList(QTr.Objects,PRr.Objects);// Edit ************************** Wednesday, 21 September 2016 11:25:49
        oldsalesman = QTr.SalesMan;
        QTr.SalesMan = PRr.Leader;
        if (nonblank(PRr.PrSalesMan)) then begin
          QTr.SalesMan = PRr.PrSalesMan;
        end;        
        QTVc_PasteSalesMan(QTr,oldsalesman);
        if (nonblank(PRr.CurncyCode)) then begin
          oldsalesman = QTr.CurncyCode;
          QTr.CurncyCode = PRr.CurncyCode;
          QTVc_PasteCurncyCode(QTr,oldsalesman);

        end;
        res = true;
      end;
    end else begin
      //QTr.Objects = AddObjectToObjectList(QTr.Objects,PRr.Objects);// Edit ************************** Wednesday, 21 September 2016 11:25:45
      res = true;

    end;
  end;  
  QTVc_PastePRCode = res;
  RETURN;
END;