remote procedure POVc_PasteTaxTemplateCode(var record POVc,Integer);
external outer function Integer TestAcceptanceStatus(Integer);// Edit ************************** Thursday, 18 October 2012 10:02:47
remote updating function LongInt RecordAction_raPastePOInDropSH(var record POVc,var record DropSHVc);
remote function Boolean POShipdTest(record POVc,Boolean);
external function roundmode DefaultRoundMode();
remote procedure POVc_PasteStockType(var record POVc,Integer);
remote procedure POVc_PasteCurncyCode(var record POVc,string,Boolean);
// The sumup functions are remote, they should be able to NOT be remote...then some of these functions would not be remote...
external function Boolean DateWarned(Date,string);
remote updating function Integer CreateVIFromPO(LongInt,record RcVc,var record VIVc);
remote function Boolean POVc_PasteLocation(var record POVc);
external function Boolean TestForMATVARINS(Integer);
external function val MulRateToBase1(var string,val,val,val,val,val,val,roundmode);
remote procedure POVc_PasteVATCode(var record POVc,Integer);
remote procedure POVc_PasteSum(var record POVc,Integer);
remote procedure POVc_PastePrice(var record POVc,Integer);
remote procedure POVc_PastevRebate(var record POVc,Integer);
remote procedure POVc_PasteVEQuant(var record POVc,Integer);
remote procedure POVc_PasteQuant(var record POVc,Integer);
remote function Boolean POVc_PasteVEArtCode(var record POVc,Integer);
remote function Boolean POVc_PasteArtCode(var record POVc,Integer,Boolean);
remote function Boolean POVc_PasteVECode(var record POVc,Boolean);
remote procedure POGetRate(var record POVc);
external procedure GetSalesGroup(string,var string);
external function Boolean AllowCurChange(string,Boolean);
remote procedure POSumup(var record POVc );
external updating procedure POCreateHtmlFile(record POVc,record MailVc);
remote updating function Integer CreateMailFromPOD(record POVc,var record MailVc,var string,string);
external procedure SendArtStat(string,string,string,val,val,val,Date,Integer);
external procedure CalcProc(val,val,var val);
external procedure FindSalesExVat(record TaxMatrixVc,string,val,Integer,Integer,var val);
external procedure VIOpenPrepExists(string);
remote updating function Boolean RecordAction_rlPOLClose(var record POVc);
remote updating function Boolean RecordAction_rlPOLOK(var record POVc);
remote updating procedure POCreate(record RcVc);
external updating procedure RecordActionPO_Print(var record POVc,string,Boolean);
remote updating function LongInt RecordAction_raPastePOInPU(var record POVc,var record PUVc);
external function Boolean PODClassCost1_5EFAfter(Integer,Integer,Integer,Integer,Integer);
external function Boolean PODClassRowCost1_5EFAfter(Integer,Integer,Integer,Integer,Integer);
external function Boolean PODClassShipCostEFAfter(Integer,Integer,Integer,Integer);
external function Boolean PODClassCustomsCostEFAfter(Integer,Integer,Integer,Integer);
remote outer function Integer SendForAcceptance_POVc(var record POVc);// Edit ************************** Thursday, 18 October 2012 10:02:36
remote outer function Integer SendForAcceptance_QTVc(var record QTVc);// Edit ************************** Thursday, 18 October 2012 10:02:35

global
updating procedure DropSHFromPODsm()
begin
  record POVc POr;
  record DropSHVc DropSHr;
  LongInt r;
  Integer wn,nwn;
  Boolean testf;
  record AcceptSetBlock AcceptSet;
  record POSettingBlock POSb;
  
  wn = CurWindow;
  if (WindowState(wn)==0) then begin//Rs_normal
    if (WindowDoOK(wn,0)==false) then begin
      goto LDropSHFromPODsm;
    end;
  end;
  if (UserCanAction("POToDropSH",true)) then begin
    BlockLoad(POSb);
    GetWindowRecord(wn,POr);
    if (POr.Closed==0) then begin
      testf = true;
      if (POr.OrderType!=kOrderTypeDropShip) then begin 
        MessageBox(22073,"");
        testf = false;
        WindowFieldGoto(wn,POr,-1,"OrderType",true);
      end;
      if (testf) then begin
        r = RecordAction_raPastePOInDropSH(POr,DropSHr);
        switch (r) begin
          case -1: Beep;
          case -2: MessageBox(1281,"");
          case 0:
            nwn = OpenWindow("DropSHDClass",1,0,"","",DropSHr);
            SetRLink(wn,true);  
          otherwise
            MessageBox(r,"");
        end;
        UpdateBrowses("POVc");
      end;       
    end else begin
      MessageBox(22062,"");
    end;
  end else begin
    MessageBox(1274,StringFromStringSet(3,"POToDropSH"));
  end;    
LDropSHFromPODsm:;
  return;
end;

function Boolean AnyNextReservationsExists() // Could get better if we want to
begin
  record StockReservVc StockReservr;
  Boolean foundf;
  Boolean res;
  
  res = false;
  StockReservr.Donef = 0;
  StockReservr.ToFileName = kResTypeNextStockIn;
  StockReservr.ToSerNr = -1;
  foundf = true;
  while (LoopKey("ToItem",StockReservr,3,foundf)) begin
    if (StockReservr.Donef!=0) then begin foundf = false; end;
    if (StockReservr.ToFileName!=kResTypeNextStockIn) then begin foundf = false; end;
    if (StockReservr.ToSerNr!=-1) then begin foundf = false; end;
    if (foundf) then begin
      res = true;
      foundf = false;
    end;
  end;
  AnyNextReservationsExists = res;
  return;
end;

global
updating procedure PUFromPODsm()
BEGIN
  record POVc POr;
  record PUVc PUr;
  LongInt r;
  Integer wn,nwn,err;
  Boolean testf;
  record POSettingBlock POSb;
  
  wn = CurWindow;
  if (WindowState(wn)==0) then begin//Rs_normal
    GetWindowRecord(wn,POr);
    switch (POr.OrderType) begin
      case kOrderTypeDropShip:
        DropSHFromPODsm;
        goto LPUFromPODsm; 
    end;
    if (UserCanAction("POToPU",true)) then begin
      BlockLoad(POSb);
      if (POr.Closed==0) then begin
        testf = true;
        if (POr.OKFlag==0) then begin 
          if ((AnyNextReservationsExists) or (POSb.POMustBeOKToPU!=0) or (UserCanAction("NotOKPOToPU",false))) then begin
            testf = false;
            MessageBox(20053,"");
          end; 
        end;
        err = TestAcceptanceStatus(POr.AcceptanceStatus);
        if (err!=0) then begin
          MessageBox(err,"");
          testf = false;
        end;
        if (testf) then begin
          r = RecordAction_raPastePOInPU(POr,PUr);
          switch (r) begin
            case -1: Beep;
            case -2: MessageBox(1281,"");
            case 0:
              nwn = OpenWindow("PUDClass",1,0,"","",PUr);
              SetRLink(wn,true);  
            otherwise
              MessageBox(r,"");
          end;
          UpdateBrowses("POVc");
        end;       
      end else begin
        MessageBox(22062,"");
      end;
    end else begin
      MessageBox(1274,StringFromStringSet(3,"POToPU"));
    end;    
  end else begin
    Beep;
  end;
LPUFromPODsm:;  
  return;
end;

global
updating function Boolean PODClassPrint(Integer wn,Boolean previewf)
begin
  record POVc POr;
  Boolean testf;
  Integer err;

  testf = true;
  DeselectWindow(wn,false);
  GetWindowRecord(wn,POr); 
  err = TestAcceptanceStatus(POr.AcceptanceStatus);
  if (err!=0) then begin
    MessageBox(err,"");
    testf = false;
  end;
  if (testf) then begin
    RecordActionPO_Print(POr,false,true);
  end;
  PODClassPrint = true;
  return;
end;

global
updating procedure PrintPOL(Integer wn,Boolean previewf)
BEGIN
  record POVc POr;
  Integer i,err;
  
  i = 1;
  while (GetRecordFromBrowse(POr,wn,i)) begin
    err = TestAcceptanceStatus(POr.AcceptanceStatus);
    if (err!=0) then begin
      MessageBox(err," " & POr.SerNr);
      goto LSKIPOrderPOL;
    end;
    RecordActionPO_Print(POr,previewf,true);
LSKIPOrderPOL:;    
    i = i + 1;
  end;
  RETURN;
END;


global
procedure POCreatePOLsm()
BEGIN
  Integer nwn,wn;
  record RcVc RepSpec;

  wn = CurWindow;
  ReportDefaults(RepSpec,"CreatePOVClass");
  nwn = OpenWindow("CreatePOVClass",1,wn,"","",RepSpec);
  DeselectWindow(nwn,false);
  RepSpec.repname = "POCreateMn";
  PutWindowRecord(nwn,RepSpec);
  RETURN;
END;

global
updating function Boolean CreatePOVClassOnOKWindow(Integer wn)
BEGIN
  record RcVc RepSpec;
  
  GetWindowRecord(wn,RepSpec);
  POCreate(RepSpec);
  CloseWindow(wn);
  CreatePOVClassOnOKWindow = false;
  RETURN;
END;

global
updating procedure PUFromPOLsm()
BEGIN
  record POVc POr;
  record PUVc PUr;
  LongInt r;
  Integer wn,nwn,err;
  Boolean testf;
  
  wn = CurWindow;
  if (UserCanAction("POToPU",true)) then begin
    if (ReadMarkedRecord(wn,POr)) then begin//Rs_normal
      testf = true;
      if (POr.OKFlag!=0) then begin 
        if (UserCanAction("NotOKPOToPU",false)) then begin
          testf = false;
        end; 
      end;
      err = TestAcceptanceStatus(POr.AcceptanceStatus);
      if (err!=0) then begin
        MessageBox(err," " & POr.SerNr);
        testf = false;
      end;
      if (testf) then begin
        r = RecordAction_raPastePOInPU(POr,PUr);
        switch (r) begin
          case -1: Beep;
          case -2: MessageBox(1281,"");
          otherwise
            nwn = OpenWindow("PUDClass",1,0,"","",PUr);
        end;
        UpdateBrowses("POVc");
      end;
    end else begin
      MessageBox(1274,StringFromStringSet(3,"POToPU"));
    end;
  end else begin
    Beep;
  end;
  RETURN;
END;

global
updating procedure OKPOLsm()
BEGIN
  record POVc POr;
  Integer i;
  Integer wn;
  
  if (UserCanAction("POOK",true)) then begin
    wn = CurWindow;
    i = 1;
    while (GetRecordFromBrowse(POr,wn,i)) begin
      if (RecordAction_rlPOLOK(POr)) then begin
      end;
      i = i + 1;
    end;
    UpdateBrowses("POVc");
  end else begin
    MessageBox(1274,StringFromStringSet(3,"POOK"));
  end;
  RETURN;
END;

global
updating procedure ClosePOLsm()
BEGIN
  record POVc POr;
  Integer i;
  Integer wn;
  
  wn = CurWindow;
  i = 1;
  while (GetRecordFromBrowse(POr,wn,i)) begin
    if (RecordAction_rlPOLClose(POr)) then begin
    end;
    i = i + 1;
  end;
  UpdateBrowses("POVc");
  RETURN;
END;



global
updating procedure VIFromPODsm()
BEGIN
  Integer wn,nwn;
  record RcVc RepSpec;
  record POVc POr;
  record POSettingBlock POSb;
  record VIVc VIr;
  Integer r;
  boolean okvitestf;

  wn = CurWindow;
  if (WindowState(wn)==0) then begin//Rs_normal
    if (UserCanAction("POToVI",true)) then begin
      BlockLoad(POSb);
      GetWindowRecord(wn,POr);
      
      okvitestf = true;
  
      if(POr.OKFlag==0)then begin
        if(!UserCanAction("UnOkPOToVI",true))then begin
          okvitestf = false;
        end;
      end;
      if(okvitestf)then begin
        ReportDefaults(RepSpec,"VIFromPOVClass");
        RepSpec.repname = "";
        RepSpec.FirstVer = POr.SerNr;
        RepSpec.flags[0] = 1;
        if (POSb.OpenCreateVIFromPO!=0) then begin
          nwn = OpenWindow("VIFromPOVClass",1,0,"","",RepSpec);
          PutWindowRecord(nwn,RepSpec);
          SelectWindow(nwn);
        end else begin
          RepSpec.flags[0] = 1;
          RepSpec.flags[1] = 1;
          RepSpec.flags[2] = 1;
          RepSpec.flags[3] = 1;
          RepSpec.flags[4] = 1;
          RepSpec.flags[5] = 1;
          RepSpec.flags[6] = 1;
          RepSpec.flags[7] = 1;
          r = CreateVIFromPO(RepSpec.FirstVer,RepSpec,VIr);
          if (r!=0) begin
            if (r>0) then begin
              MessageBox(r,"");
            end else begin
              Beep;
            end;
          end else begin
            nwn = OpenWindow("VIDClass",1,0,"","",VIr);
            VIOpenPrepExists(VIr.VECode);        
            UpdateBrowses("POVc");    
            if (DateWarned(VIr.TransDate,"VIVc")) then begin
              MessageBox(1045,"");
            end;
          end;      
        end;
      end else begin
        MessageBox(1274,StringFromStringSet(3,"UnOkPOToVI"));
      end;
    end else begin
      MessageBox(1274,StringFromStringSet(3,"POToVI"));
    end;
  end else begin
    Beep;
  end;
  RETURN;
END;

global
updating function Boolean VIFromPOVClassOnOKWindow(Integer wn)
BEGIN
  record RcVc RepSpec;
  record VIVc VIr;
  Integer nwn;
  Integer r;
  
  GetWindowRecord(wn,RepSpec);
  if (blank(RepSpec.f1)) then begin
    if (RepSpec.flags[0]==0) then begin
      if ((RepSpec.flags[1]!=0) or (RepSpec.flags[2]!=0) or (RepSpec.flags[3]!=0) or (RepSpec.flags[4]!=0) or (RepSpec.flags[5]!=0) or (RepSpec.flags[6]!=0) or (RepSpec.flags[7]!=0)) then begin
        MessageBox(0,USetStr(10426));
        WindowFieldGoto(wn,RepSpec,-1,"f1",true);        
        goto LVIFromPOVClassOnOKWindow;
      end;
    end;
  end;
  r = CreateVIFromPO(RepSpec.FirstVer,RepSpec,VIr);
  if (r!=0) begin
    if (r>0) then begin
      MessageBox(r,"");
    end else begin
      Beep;
    end;
  end else begin
    nwn = OpenWindow("VIDClass",1,0,"","",VIr);
    VIOpenPrepExists(VIr.VECode);        
    UpdateBrowses("POVc");    
  end;
  CloseWindow(wn);
LVIFromPOVClassOnOKWindow:;  
  VIFromPOVClassOnOKWindow = false;
  RETURN;
END;

procedure PODSwitchRow(Integer wn,Integer rownr)
BEGIN
  record POVc POr;  
  row POVc POrw; 
  Integer rwcnt;
  Boolean res;
  val t,tproc,unitprdisc,s,rowsum;
  record TaxMatrixVc TMr;

  res = true;
  if (rownr>=0) then begin
    GetWindowRecord(wn,POr);
    rwcnt = MatRowCnt(POr);  
    if (rownr<rwcnt) then begin
      MatRowGet(POr,rownr,POrw);
      if (POrw.stp==1) then begin
        s = MulRateToBase1(POr.CurncyCode,POrw.Sum,POr.FrRate,POr.ToRateB1,POr.ToRateB2,POr.BaseRate1,POr.BaseRate2,DefaultCurRoundOff);
        //UnpackRowFieldMatrix(POrw,"TaxMatrix",TMr);
        FindSalesExVat(TMr,POrw.VATCode,s,POr.InclVAT,0,rowsum);
        CalcProc(rowsum,t,tproc);
        unitprdisc = POrw.Sum/POrw.Quant;
        unitprdisc = Round(unitprdisc,DefaultRoundMode);
      end;
      SendArtStat(POrw.ArtCode,POr.Location,"",0,tproc,unitprdisc,POr.TransDate,1);
    end;
  end;
  SetWindowNameArg(wn,POrw.ArtCode & ":" & POr.Location);
  RETURN;
END;

global
function Boolean PODClassSwitchRow(Integer wn,Integer rownr)
begin        
  PODSwitchRow(wn,rownr);
  PODClassSwitchRow = true;  
  return;
end;

global
procedure ItemStatusPODsm()
BEGIN
  Integer wn,nwn;
  record RcVc RepSpec;
  
  wn = CurWindow;
  nwn = OpenWindow("ArtStatIClass",0,0,"","",RepSpec);
  SelectWindow(wn);
  PODSwitchRow(wn,WindowActiveRow(wn));
  RETURN;
END;

global 
updating procedure CreateMailFromPODsm()
BEGIN
  Integer wn,nwn;
  record MailVc Mailr;
  record POVc POr;
  Integer err;
  string 255 tstr;
  Boolean testf;
  string 255 docname;
	
  wn = CurWindow;
  if (WindowState(wn)==0) then begin//Rs_normal
    GetWindowRecord(wn,POr);  
    testf = true;
    if (POr.OKFlag==0) then begin
      if (UserCanAction("EMailFromOKPO",false)) then begin testf = false; end;
    end;
    err = TestAcceptanceStatus(POr.AcceptanceStatus);
    if (err!=0) then begin
      testf = false;
    end;
    if (testf) then begin
      err = CreateMailFromPOD(POr,Mailr,tstr,WindowFormName(wn));
      if (err!=0) then begin
        MessageBox(err,": " & tstr);
      end else begin
        nwn = OpenWindow("MailDClass",1,0,"","",Mailr);
      end;
    end;
  end;
  RETURN;
END;

global
procedure SubtotalPODsm()
BEGIN
  record POVc POr;
  row POVc POrw;
  Integer wn,i,rwcnt,rownr;
  val pt;

  wn = CurWindow;
  GetWindowRecord(wn,POr);
  rownr = WindowActiveRow(wn);
  DeselectWindow(wn,false);
  if (rownr==-1) then begin
    rwcnt = MatRowCnt(POr);
  end else begin
    rwcnt = rownr + 1;
  end;
  for (i=rwcnt-1;i>=0;i=i-1) begin
    MatRowGet(POr,i,POrw);
    if (POrw.stp!=9) then begin
      pt = pt + POrw.Sum;
    end else begin
      if (POrw.Sum!=0) then begin goto LBREAK; end;
    end;
  end;
LBREAK:;  
  ClearRow(POr,POrw,9);
  POrw.Sum = pt;
  MatRowInsert(POr,rwcnt,POrw);
  PutWindowRecord(wn,POr);
  RETURN;
END;

global
procedure POStatRnPODsm()
BEGIN
  record RcVc RepSpec;
  record POVc POr;

  GetWindowRecord(CurWindow,POr);
  RepSpec.f1 = POr.SerNr;
  RepSpec.sStartDate = POr.TransDate;
  RepSpec.sEndDate = POr.TransDate;
  RepSpec.ArtMode = 1;   //Detailed
  RepSpec.flags[4] = 1;  //Indicates that report was run from Special Menu opf PODClass
  RepSpec.flags[3] = 1;  //include Not Oked
  RepSpec.flags[1] = 1;  //Include Closed
  RepSpec.Media = mtScreen;
  RepSpec.repname = "POStatRn";
  RunReport(RepSpec,0);
  RETURN;
END;

global
procedure POStatusORLsm()
BEGIN
  record RcVc RepSpec;
  record POVc POr;
  
  if (ReadMarkedRecord(CurWindow,POr)) then begin
    RepSpec.f1 = POr.SerNr;
    RepSpec.sStartDate=POr.TransDate;
    RepSpec.sEndDate=POr.TransDate;
    RepSpec.ArtMode=1;   //Detailed
    RepSpec.flags[4]=1;  //Indicates that report was run from Special Menu opf PODClass
    RepSpec.flags[3]=1;  //include Not Oked
    RepSpec.flags[1]=1;  //Include Closed
    RepSpec.Media = mtScreen;
    RepSpec.repname = "POStatRn";
    RunReport(RepSpec,0);
  end;
  RETURN;
END;

global
function Boolean PODClassInclVATButtonAction(Integer wn,Integer value)
BEGIN
  Boolean res;
  record POVc POr;
  Integer normalmode,updatemode;
 
  res = true;
  normalmode = 0;//Rs_normal
  updatemode = 2;//Rs_update
  if (WindowState(wn)==normalmode) then begin
    GetWindowRecord(wn,POr);
    if (POr.Closed!=0) then begin
      res = false;
    end;
  end;  
  if (WindowState(wn)==updatemode) then begin
    GetPrevWindowRecord(wn,POr);
    if (POr.Closed!=0) then begin
      res = false;
    end;
  end;  
  PODClassInclVATButtonAction = res;
  RETURN;
END;

global
function Boolean PODClassClosedButtonAction(Integer wn,Integer value)
BEGIN
  Boolean res;
  record POVc POr;
  Integer normalmode,updatemode;
 
  res = true;
  normalmode = 0;//Rs_normal
  updatemode = 2;//Rs_update
  if (WindowState(wn)==normalmode) then begin
    GetWindowRecord(wn,POr);
    if (POr.Closed!=0) then begin
      res = false;
    end;
  end;  
  if (WindowState(wn)==updatemode) then begin
    GetPrevWindowRecord(wn,POr);
    if (POr.Closed!=0) then begin
      res = false;
    end;
  end;  
  PODClassClosedButtonAction = res;
  RETURN;
END;

function Boolean POAcceptanceStarted(record POVc POr)
begin
  Boolean res;

  res = false;
  if (POr.AcceptanceStatus>=kAcceptanceStatePending) then begin
    res = true;
  end;
  POAcceptanceStarted = res;
  return;
end;

global
function Boolean PODClassOKFlagButtonAction(Integer wn,Integer value)
BEGIN
  Boolean res;
  record POVc POr;
  Integer normalmode,updatemode;
  record POSettingBlock POSettingr;
  Integer err;
 
  BlockLoad(POSettingr);
  res = true;
  normalmode = 0;//Rs_normal
  updatemode = 2;//Rs_update
  if (WindowState(wn)==Rs_insert) then begin
    GetWindowRecord(wn,POr);
    err = TestAcceptanceStatus(POr.AcceptanceStatus);
    if (err!=0) then begin
      MessageBox(err,"");
      res = false;
      goto LPODClassOKFlagButtonAction;
    end;
  end;
  if (WindowState(wn)==Rs_normal) then begin
    GetWindowRecord(wn,POr);
    if (POr.Closed!=0) then begin
      res = false;
    end;
    err = TestAcceptanceStatus(POr.AcceptanceStatus);
    if (err!=0) then begin
      MessageBox(err,"");
      res = false;
      goto LPODClassOKFlagButtonAction;
    end;
    if (POSettingr.POMustBeOKToPU!=0) then begin  
      if (POr.OKFlag!=0) then begin
        res = false;
      end;
    end;
  end;  
  if (WindowState(wn)==updatemode) then begin
    GetWindowRecord(wn,POr);
    if (POr.Closed!=0) then begin
      res = false;
    end;
    err = TestAcceptanceStatus(POr.AcceptanceStatus);
    if (err!=0) then begin
      MessageBox(err,"");
      res = false;
      goto LPODClassOKFlagButtonAction;
    end;
  end; 
LPODClassOKFlagButtonAction:;   
  PODClassOKFlagButtonAction = res;
  RETURN;
END;

global
function Boolean PODClassExportFlagButtonAfter(Integer wn,Boolean changedf)
begin        
  record POVc POr;  
  Boolean res;

  GetWindowRecord(wn,POr);
  POSumup(POr);
  PutWindowRecord(wn,POr);
  PODClassExportFlagButtonAfter = res;  
  return;
end;

function Boolean POShipdFromRowTest(record POVc POp,Integer rownr)
begin
  Boolean res;
  Integer i,rwcnt;
  row POVc POrw;
  
  rwcnt = MatRowCnt(POp);
  for (i=rownr;i<rwcnt;i=i+1) begin
    MatRowGet(POp,i,POrw);
    if (POrw.Shipd1>0) or (POrw.Invd>0) then begin
      res = true;
      goto LPOShipdFromRowTest;
    end;
  end;
LPOShipdFromRowTest:;
  POShipdFromRowTest = res;
  return;
end;

global 
function Boolean PODClassDeleteRowTest(Integer wn,Integer rownr)
begin
  record POVc POr;
  record POVc PO2r;
  Boolean res;

  res = true;
  GetWindowRecord(wn,POr);
  if (POr.OrderType==kOrderTypeDropShip) then begin 
    res = false;
    goto LPODClassDeleteRowTest;
  end;
  if (WindowState(wn)==Rs_update) then begin
    GetPrevWindowRecord(wn,PO2r);
    if (POAcceptanceStarted(POr)) then begin
      res = false;
      goto LPODClassDeleteRowTest;
    end;
    if (POShipdFromRowTest(POr,rownr)) then begin
      res = false;
      MessageBox(1304,"");
    end;
    if (PO2r.OKFlag) then begin
      res = false;
      MessageBox(20589,"");
    end;
  end;
LPODClassDeleteRowTest:;  
  PODClassDeleteRowTest = res;
  return;
end;

global 
function Boolean PODClassInsertRowTest(Integer wn,Integer rownr)
begin
  record POVc POr;
  record POVc PO2r;
  Boolean res;

  res = false;  
  GetWindowRecord(wn,POr);
  if (POr.Closed==0) then begin
    res = true;  
    if (POAcceptanceStarted(POr)) then begin
      res = false;
      goto LPODClassInsertRowTest;
    end;
    if (POr.OrderType==kOrderTypeDropShip) then begin 
      res = false;
      goto LPODClassInsertRowTest;
    end;
    switch (WindowState(wn)) begin
      case 0://Rs_normal
        if (POShipdTest(POr,true)) or (POr.OKFlag!=0) then begin res = false; end;
      case 1://Rs_insert
        res = true;
      case 2://Rs_update
        GetPrevWindowRecord(wn,PO2r);
        if (POShipdTest(PO2r,true)) or (POr.OKFlag!=0) then begin res = false; end;
      otherwise
        res = false;
    end;
  end;  
LPODClassInsertRowTest:;  
  PODClassInsertRowTest = res;
  return;
end;

global
function Boolean PODClassOnOverStrike(Integer wn,Integer rownr)
begin
  record POVc POr;

  if (rownr>=0) then begin
    GetWindowRecord(wn,POr);    
    POSumup(POr);
    PutWindowRecord(wn,POr);    
  end;
  PODClassOnOverStrike = true;
  return;
end;

global
function Boolean PODClassActiveEditField(Integer wn,string fieldname,Integer fn,Integer wnst,Integer rownr,Integer changed)
begin
  Boolean res;
  record POVc POr;
  record POVc PO2r;
  record MainStockBlock MSb;
  record INVc INr;
  row POVc POrw;
  
  res = true;
  GetWindowRecord(wn,POr);    
  switch (fieldname) begin
    case "SerNr":
      switch (wnst) begin
        case Rs_insert:
          if (UserCanAction("AllowPOSerNrChange",true)==false) then begin res = false; end;
        otherwise
           res = false;
           goto LPODClassActiveEditField;
      end;
    case "FrRate": 
      if (AllowCurChange(POr.CurncyCode,false)==false) then begin res = false; end;
    case "ToRateB1": 
      if (AllowCurChange(POr.CurncyCode,false)==false) then begin res = false; end;
    case "ToRateB2": 
      if (AllowCurChange(POr.CurncyCode,false)==false) then begin res = false; end;
    case "BaseRate1": 
      if (AllowCurChange(POr.CurncyCode,true)==false) then begin res = false; end;
    case "BaseRate2": 
      if (AllowCurChange(POr.CurncyCode,true)==false) then begin res = false; end;  
    case "Shipd1": 
       res = false;
    case "Shipd2": 
       MatRowGet(POr,rownr,POrw);
       if (POrw.Shipd2<=POrw.Quant) then begin
         BlockLoad(MSb);
         switch (MSb.RecevPlainItems) begin
           case 2:
             res = false;
             INr.Code = POrw.ArtCode;
             if (ReadFIrstMain(INr,1,true)) then begin
               if (INr.ItemType==0) or (INr.ItemType==3) then begin
                 res = true;
               end;
             end;
           otherwise
             res = false;
         end;
       end;
    case "Invd": 
       res = false;
  end;
  if (res==false) then begin
    goto LPODClassActiveEditField;
  end;
  if (wnst==2) then begin//Rs_update
    GetPrevWindowRecord(wn,PO2r);
    if (POAcceptanceStarted(POr)) then begin res = false; goto LPODClassActiveEditField; end;
    if (PO2r.OKFlag!=0) then begin
      switch (fieldname) begin
        case "PlanShip": res = true;
        case "LangCode": res = true;
        otherwise res = false;
      end;
      if (rownr>=0) then begin
        res = true;
        switch (fieldname) begin
          case "PlanShipRow":
            res = true;
          otherwise
            res = false;
            goto LPODClassActiveEditField;
        end;
      end else begin
        if (res==false) then begin goto LPODClassActiveEditField; end;
      end;      
    end else begin
      if (POr.OrderType==kOrderTypeDropShip) then begin 
        switch (fieldname) begin
          case "ArtCode": res = false;
          case "Quant": res = false;
        end;
        if (res==false) then begin goto LPODClassActiveEditField; end;
      end;
      if (POShipdTest(POr,true)==false) then begin goto LPODClassActiveEditField; end;
      if (rownr<WindowOldRowcnt(wn)) then begin
        switch (fieldname) begin
          case "ArtCode":
            res = false;
            if (changed!=0) then begin MessageBox(1304,""); end;
            goto LPODClassActiveEditField;          
          case "StockType":
            res = false;
            goto LPODClassActiveEditField;          
        end;
      end;
    end;
  end;
LPODClassActiveEditField:;  
  PODClassActiveEditField = res;
  return;
end;

function Boolean PODClassPlanShipEFAfter(Integer wn,Boolean changedf)
begin
  record POVc POr;
  record PlanDeliveryBlock PlanDelRec;
  LongInt week;
  date d;
  
  if (changedf) then begin
    BlockLoad(PlanDelRec);
    GetWindowRecord(wn,POr);
    switch (PlanDelRec.FieldType) begin
      case 1:  /* date */
        d = POr.PlanShip;
        POr.PlanShip = d;
        POr.PlanShipDate = StringToDate(POr.PlanShip);
      case 2:  /* week number (nn) */
        week = POr.PlanShip;
        POr.PlanShip = week;
      case 3:  /* week number (yynn */
        if (nonblank(POr.PlanShip)) then begin
          week = POr.PlanShip;
          POr.PlanShip = week;
          if (len(POr.PlanShip)<4) then begin
            POr.PlanShip = "0" & POr.PlanShip;
          end;
          if (len(POr.PlanShip)>4) then begin
            POr.PlanShip = Left(POr.PlanShip,4);
          end;
        end;
    end;
    PutWindowRecord(wn,POr);    
  end;
  PODClassPlanShipEFAfter = true;
  return;
end;

function Boolean PODClassSalesManEFAfter(Integer wn,Boolean changedf)
BEGIN
  record POVc POr;
  string 255 tstr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    GetSalesGroup(POr.SalesMan,tstr);
    POr.SalesGroup = tstr;
    PutWindowRecord(wn,POr);
  end;
  PODClassSalesManEFAfter = true;
  return;
end;

function Boolean PODClassCurncyCodeEFAfter(Integer wn,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    POVc_PasteCurncyCode(POr,WindEFstr(wn),true);    
    PutWindowRecord(wn,POr);
  end;
  PODClassCurncyCodeEFAfter = true;
  return;
end;

function Boolean PODClassTransDateEFAfter(Integer wn,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    POGetRate(POr);
    PutWindowRecord(wn,POr);
  end;
  PODClassTransDateEFAfter = true;
  return;
end;

function Boolean PODClassLocationEFAfter(Integer wn,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    if (POVc_PasteLocation(POr)==false) then begin
      Beep;
    end;
    PutWindowRecord(wn,POr);
  end;
  PODClassLocationEFAfter = true;
  return;
end;

function Boolean PODClassVECodeEFAfter(Integer wn,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    if (POVc_PasteVECode(POr,true)) then begin
      PutWindowRecord(wn,POr);
    end else begin
      Beep;
    end;
  end;
  PODClassVECodeEFAfter = true;
  return;
end;

function Boolean PODClassPRCodeEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record POVc POr;
  row POVc POrw;
  record PRVc PRr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);   
    if (rownr<0) then begin
      if (nonblank(POr.PRCode)) then begin
        PRr.Code = POr.PRCode;
        if (ReadFirstMain(PRr,1,true)) then begin
          if (nonblank(POr.Objects)) then begin
            POr.Objects = POr.Objects & ",";
          end;
          POr.Objects = POr.Objects & PRr.Objects;
        end;
      end;
    end else begin
      MatRowGet(POr,rownr,POrw);
      if (nonblank(POrw.PRCode)) then begin
        PRr.Code = POrw.PRCode;
        if (ReadFirstMain(PRr,1,true)) then begin
          if (nonblank(POrw.Objects)) then begin
            POrw.Objects = POrw.Objects & ",";
          end;
          POrw.Objects = POrw.Objects & PRr.Objects;
        end;
      end;
      MatRowPut(POr,rownr,POrw);
    end; 
    PutWindowRecord(wn,POr);   
  end;
  PODClassPRCodeEFAfter = true;
  return;
end;

function Boolean PODClassPlanShipRowEFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record POVc POr;
  row POVc POrw;
  record PlanDeliveryBlock PlanDelRec;
  LongInt week;
  date d;
  
  if (changedf) then begin
    BlockLoad(PlanDelRec);
    GetWindowRecord(wn,POr);
    MatRowGet(POr,rownr,POrw);
    switch (PlanDelRec.FieldType) begin
      case 1:  /* date */
        d = POrw.PlanShipRow;
        POrw.PlanShipRow = d;
      case 2:  /* week number (nn) */
        week = POrw.PlanShipRow;
        POrw.PlanShipRow = week;
      case 3:  /* week number (yynn */
        week = POrw.PlanShipRow;
        POrw.PlanShipRow = week;
        if (len(POrw.PlanShipRow)<4) then begin
          POrw.PlanShipRow = "0" & POrw.PlanShipRow;
        end;
        if (len(POrw.PlanShipRow)>4) then begin
          POrw.PlanShipRow = Left(POrw.PlanShipRow,4);
        end;
    end;
    MatRowPut(POr,rownr,POrw);
    PutWindowRecord(wn,POr);    
  end;
  PODClassPlanShipRowEFAfter = true;
  return;
end;

function Boolean PODClassArtCodeEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record POVc POr;
  row POVc POrw;

  GetWindowRecord(wn,POr);    
  MatRowGet(POr,rownr,POrw);
  if ((changedf) or (blank(POrw.Spec) and (nonblank(POrw.ArtCode)))) then begin
    if (POVc_PasteArtCode(POr,rownr,false)) then begin
      PutWindowRecord(wn,POr);
      if (PODClassSwitchRow(wn,rownr)) then begin end;
    end else begin
      Beep;
    end;
  end;
  PODClassArtCodeEFAfter = true;
  return;
end;

function Boolean PODClassVEArtCodeEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    if (POVc_PasteVEArtCode(POr,rownr)) then begin
      PutWindowRecord(wn,POr);
    end else begin
      Beep;
    end;
  end;
  PODClassVEArtCodeEFAfter = true;
  return;
end;

function Boolean PODClassQuantEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    POVc_PasteQuant(POr,rownr);
    PutWindowRecord(wn,POr);
  end;
  PODClassQuantEFAfter = true;
  return;
end;

function Boolean PODClassVEQuantEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    POVc_PasteVEQuant(POr,rownr);
    PutWindowRecord(wn,POr);
  end;
  PODClassVEQuantEFAfter = true;
  return;
end;

function Boolean PODClassPriceEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    POVc_PastePrice(POr,rownr);
    PutWindowRecord(wn,POr);
  end;
  PODClassPriceEFAfter = true;
  return;
end;

function Boolean PODClassvRebateEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    POVc_PastevRebate(POr,rownr);
    PutWindowRecord(wn,POr);
  end;
  PODClassvRebateEFAfter = true;
  return;
end;

function Boolean PODClassSumEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    POVc_PasteSum(POr,rownr);
    PutWindowRecord(wn,POr);
  end;
  PODClassSumEFAfter = true;
  return;
end;

function Boolean PODClassVATCodeEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    POVc_PasteVATCode(POr,rownr);
    PutWindowRecord(wn,POr);
  end;
  PODClassVATCodeEFAfter = true;
  return;
end;

function Boolean PODClassTaxTemplateCodeEFAfter(Integer wn,Integer rownr,Boolean changedf)
BEGIN
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    POVc_PasteTaxTemplateCode(POr,rownr);
    PutWindowRecord(wn,POr);
  end;
  PODClassTaxTemplateCodeEFAfter = true;
  return;
end;

function Boolean PODClassStockTypeEFAfter(Integer wn,Integer rownr,Boolean changedf)
begin
  record POVc POr;

  if (changedf) then begin
    GetWindowRecord(wn,POr);    
    POVc_PasteStockType(POr,rownr);
    PutWindowRecord(wn,POr);
  end;
  PODClassStockTypeEFAfter = true;
  return;
end;

global
function Boolean PODClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;

  switch (fieldname) begin
    case "PlanShip": res = PODClassPlanShipEFAfter(wn,changed!=0);
    case "SalesMan": res = PODClassSalesManEFAfter(wn,changed!=0);
    case "CurncyCode": res = PODClassCurncyCodeEFAfter(wn,changed!=0);
    case "TransDate": res = PODClassTransDateEFAfter(wn,changed!=0);
    case "Location": res = PODClassLocationEFAfter(wn,changed!=0);
    case "VECode": res = PODClassVECodeEFAfter(wn,changed!=0);
    case "PRCode": res = PODClassPRCodeEFAfter(wn,rownr,changed!=0);

    case "PlanShipRow": res = PODClassPlanShipRowEFAfter(wn,rownr,changed!=0);
    case "ArtCode": res = PODClassArtCodeEFAfter(wn,rownr,changed!=0);
    case "VEArtCode": res = PODClassVEArtCodeEFAfter(wn,rownr,changed!=0);
    case "Quant": res = PODClassQuantEFAfter(wn,rownr,changed!=0);
    case "VEQuant": res = PODClassVEQuantEFAfter(wn,rownr,changed!=0);
    case "Price": res = PODClassPriceEFAfter(wn,rownr,changed!=0);
    case "vRebate": res = PODClassvRebateEFAfter(wn,rownr,changed!=0);
    case "Sum": res = PODClassSumEFAfter(wn,rownr,changed!=0);
    case "VATCode": res = PODClassVATCodeEFAfter(wn,rownr,changed!=0);
    case "TaxTemplateCode": res = PODClassTaxTemplateCodeEFAfter(wn,rownr,changed!=0);
    
    case "Cost1": res = PODClassCost1_5EFAfter(wn,fn,rownr,changed,1);
    case "Cost2": res = PODClassCost1_5EFAfter(wn,fn,rownr,changed,2);
    case "Cost3": res = PODClassCost1_5EFAfter(wn,fn,rownr,changed,3);
    case "Cost4": res = PODClassCost1_5EFAfter(wn,fn,rownr,changed,4);
    case "Cost5": res = PODClassCost1_5EFAfter(wn,fn,rownr,changed,5);
    case "RowCost1": res = PODClassRowCost1_5EFAfter(wn,fn,rownr,changed,1);
    case "RowCost2": res = PODClassRowCost1_5EFAfter(wn,fn,rownr,changed,2);
    case "RowCost3": res = PODClassRowCost1_5EFAfter(wn,fn,rownr,changed,3);
    case "RowCost4": res = PODClassRowCost1_5EFAfter(wn,fn,rownr,changed,4);
    case "RowCost5": res = PODClassRowCost1_5EFAfter(wn,fn,rownr,changed,5);
    case "ShipCost": res = PODClassShipCostEFAfter(wn,fn,rownr,changed);
    case "CustomsCost": res = PODClassCustomsCostEFAfter(wn,fn,rownr,changed);
    case "StockType": res = PODClassStockTypeEFAfter(wn,rownr,changed!=0);
  end;
  PODClassAfterEditField = res;
  RETURN;
END;

global
function Boolean PODClassBeforeEditField(Integer wn,string fieldname,Integer fn, Integer rownr)
BEGIN
  Boolean res;
  record POVc POr;
  row POVc POrw;

  switch (fieldname) begin  
    case "Quant":     
      GetWindowRecord(wn,POr);      
      MatRowGet(POr,rownr,POrw);
      if (POrw.Quant==0) then begin
        if (TestForMATVARINS(wn)) then begin end;
      end;
  end;
  PODClassBeforeEditField = res;
  return;
end;

global
function Boolean PODClassExportFlagButtonAction(Integer wn,Integer value)
BEGIN
  Boolean res;
//vat zone should be as it is on customer card
//otherwise u can paste customer  , filled all, change vat zone and get crap
  res = false;
  PODClassExportFlagButtonAction = res;
  RETURN;
END;

global
function Boolean PODClassAcceptanceStatusButtonAction(Integer wn,Integer value)
BEGIN
  Boolean res;

  res = false;
  PODClassAcceptanceStatusButtonAction = res;
  RETURN;
END;

procedure PODClassSpecPasteNameArtCode(Integer wn,var string psname)
begin
  record POVc POr;
  Integer rownr;

  GetWindowRecord(wn,POr);
  rownr = WindowActiveRow(wn);
  DeselectWindow(wn,false);//to get VARINSClass working
  WindowFieldGoto(wn,POr,rownr,"ArtCode",false);
  return;
end;

global
function string 40 PODClassSpecPasteName(Integer wn,string defpsname)
begin
  string 40 psname;

  psname = defpsname;
  switch (WindowActiveField(wn)) begin
    case "ArtCode": PODClassSpecPasteNameArtCode(wn,psname);
  end;
  PODClassSpecPasteName = psname;
  return;
end;



global
updating procedure POSendforAcceptancePODsm()
begin
  Integer wn,err;
  record POVc POr;
  
  wn = CurWindow;
  GetWindowRecord(wn,POr);
  if (WindowState(wn)!=Rs_normal) then begin
    if (WindowDoOK(wn,0)==false) then begin
      goto LPOSendforAcceptancePODsm;
    end;
  end;
  switch (POr.AcceptanceStatus) begin
    case kAcceptanceStateNotRequired:
      MessageBox(22404,"");
      goto LPOSendforAcceptancePODsm;
    case kAcceptanceStatePending:
      MessageBox(22400,"");
      goto LPOSendforAcceptancePODsm;
    case kAcceptanceStateApproved:
      MessageBox(22400,"");
      goto LPOSendforAcceptancePODsm;
    case kAcceptanceStateRejected:
      MessageBox(22400,"");
      goto LPOSendforAcceptancePODsm;
  end;
  err = SendForAcceptance_POVc(POr);
  switch (err) begin  
    case 0:
      PutWindowRecord(wn,POr);
      if (WindowDoOK(wn,0)) then begin
      end;
    otherwise
      MessageBox(err,"");
  end;
LPOSendforAcceptancePODsm:;  
  return;
end;


global
updating procedure QTSendforAcceptanceQTDsm()
begin
  Integer wn,err;
  record QTVc QTr,origQTr;// Edit ************************** Friday, 31 May 2013 10:19:26
  
  wn = CurWindow;
  GetWindowRecord(wn,origQTr);// Edit ************************** Friday, 31 May 2013 10:19:29
  QTr.SerNr = origQTr.SerNr;// Edit ************************** Friday, 31 May 2013 10:19:27
  if(readfirstmain(QTr,1,true))then begin// Edit ************************** Friday, 31 May 2013 10:19:25
		if (WindowState(wn)!=Rs_normal) then begin
			 Beep;
			//if (WindowDoOK(wn,0)==false) then begin
				goto LQTSendforAcceptanceQTDsm;
			//end;
		end;
			
		switch (QTr.AcceptanceStatus) begin
			case kAcceptanceStateNotRequired:
				MessageBox(22404,"");
				goto LQTSendforAcceptanceQTDsm;
			case kAcceptanceStatePending:
				MessageBox(22400,"");
				goto LQTSendforAcceptanceQTDsm;
			case kAcceptanceStateApproved:
				MessageBox(22400,"");
				goto LQTSendforAcceptanceQTDsm;
			case kAcceptanceStateRejected:
				MessageBox(22400,"");
				goto LQTSendforAcceptanceQTDsm;
		end;
		err = SendForAcceptance_QTVc(QTr);
		switch (err) begin  
			case 0:
				PutWindowRecord(wn,QTr);
				if (WindowDoOK(wn,0)) then begin
				end;
			otherwise
				MessageBox(err,"");
		end;
  end;// Edit ************************** Friday, 31 May 2013 10:19:32
  
  
LQTSendforAcceptanceQTDsm:;  
  return;
end;
